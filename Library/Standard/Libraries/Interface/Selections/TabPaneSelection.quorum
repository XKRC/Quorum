package Libraries.Interface.Selections

use Libraries.Interface.Controls.Tab
use Libraries.Interface.Controls.TabPane

class TabPaneSelection is Selection

    integer index = -1

    action Initialize(TabPane pane, integer index)
        text value = ""
        me:index = index
        Tab tab = GetTab()
        if tab not= undefined
            value = tab:GetName()
        end
        parent:Selection:Initialize(pane, value)
    end

    action Set(integer index)
        me:index = index
        Tab tab = GetTab()
        if tab not= undefined
            SetDisplayName(tab:GetName())
        else
            SetDisplayName("")
        end
    end

    action GetIndex returns integer
        return index
    end

    action GetTab returns Tab
        TabPane pane = GetTabPane()
        if index >= 0 and pane not= undefined and index < pane:GetSize()
            return pane:Get(index)
        end
        return undefined
    end

    action GetTabPane returns TabPane
        if GetItem() is TabPane
            TabPane pane = cast(TabPane, GetItem())
            return pane
        end
        return undefined
    end

    action IsEmpty returns boolean
        return index = -1
    end
end