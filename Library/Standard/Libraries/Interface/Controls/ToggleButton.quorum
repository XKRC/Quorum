package Libraries.Interface.Controls

use Libraries.Interface.Behaviors.Behavior
use Libraries.Interface.Events.BehaviorEvent
use Libraries.Interface.Views.ToggleView

class ToggleButton is Button
    
    boolean toggled = false
    boolean canToggleOff = true
    ButtonGroup group = undefined

    on create
        SetAccessibilityCode(parent:Item:TOGGLE_BUTTON)
    end

    action SetToggleState(boolean toggleState)
        if toggled = toggleState
            return now
        end

        if toggleState = true and group not= undefined
            group:DeselectAll()
        end

        toggled = toggleState
        if GetView2D() is ToggleView
            ToggleView content = cast(ToggleView, GetView2D())
            content:SetToggleState(toggleState)
        end

        Behavior behavior = GetActivationBehavior()
        if toggleState = true
            OnToggleOn()
            if behavior not= undefined
                BehaviorEvent event
                event:SetItem(me)
                behavior:Run(event)
            end
        else
            OnToggleOff()
            if behavior not= undefined
                behavior:Dispose()
            end
        end
    end

    action GetToggleState returns boolean
        return toggled
    end

    action Update(number seconds)
        Behavior behavior = GetActivationBehavior()
        if toggled and behavior not= undefined
            behavior:Update(seconds)
        end

        parent:Libraries.Interface.Item2D:Update(seconds)
    end

    action ReleasedMouse
        if toggled
            if canToggleOff
                SetToggleState(false)
            end
        else
            SetToggleState(true)
        end
        parent:Control:ReleasedMouse()
    end

    action SetButtonGroup(ButtonGroup buttonGroup)
        group = buttonGroup
    end

    action GetButtonGroup returns ButtonGroup
        return group
    end

    action AllowToggleOff(boolean canToggleOff)
        me:canToggleOff = canToggleOff
    end

    action CanToggleOff returns boolean
        return canToggleOff
    end

    action OnToggleOn
        
    end

    action OnToggleOff

    end
end