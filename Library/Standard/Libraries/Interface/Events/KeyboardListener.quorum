package Libraries.Interface.Events

/*
The KeyboardListener class is used to listen for and react to KeyboardEvents.
Programs that want to react to KeyboardEvents should make a class that inherits
from this class and overrides the PressedKey and/or ReleasedKey actions.
*/
class KeyboardListener

    /*
    This action responds to a KeyboardEvent where the user pressed a key. This
    action does nothing on its own, and should be overriden in an inheriting
    class for user programs.

    Attribute: Parameter event The KeyboardEvent caused by pressing a key.

    Attribute: Example

    use Libraries.Game.Game
    use Libraries.Interface.Events.KeyboardEvent
    use Libraries.Interface.Events.KeyboardListener

    class Main is Game, KeyboardListener

        integer counter = 0

        action Main
            StartGame()
        end

        action CreateGame
            AddKeyboardListener(me)
        end

        action PressedKey(KeyboardEvent event)
            if event:keyCode = event:SPACE
                counter = counter + 1
                output "The space bar has been pressed " + counter + " times."
            end
        end
    end
    */
    action PressedKey(KeyboardEvent event)

    end

    /*
    This action responds to a KeyboardEvent where the user released a key. This
    action does nothing on its own, and should be overriden in an inheriting
    class for user programs.

    Attribute: Parameter event The KeyboardEvent caused by releasing a key.

    Attribute: Example

    use Libraries.Game.Game
    use Libraries.Interface.Events.KeyboardEvent
    use Libraries.Interface.Events.KeyboardListener

    class Main is Game, KeyboardListener

        integer counter = 0

        action Main
            StartGame()
        end

        action CreateGame
            AddKeyboardListener(me)
        end

        action ReleasedKey(KeyboardEvent event)
            if event:keyCode = event:SPACE
                counter = counter + 1
                output "The space bar has been released " + counter + " times."
            end
        end
    end
    */
    action ReleasedKey(KeyboardEvent event)

    end

end