package Libraries.Interface.Events

/*
The MouseMovementListener class is used to listen for and react to MouseEvents
where the user has moved or dragged the mouse. To use a MouseMovementListener in
a program, users should make a new class that inherits from it and overrides
MovedMouse and/or DraggedMouse.
*/
class MouseMovementListener

    /*
    This action is used to respond to a MouseEvent created by the mouse moving.

    Attribute: Parameter event A MouseEvent describing the mouse movement.

    Attribute: Example

    use Libraries.Game.Game
    use Libraries.Interface.Events.MouseEvent
    use Libraries.Interface.Events.MouseMovementListener
    use Libraries.Game.Graphics.Label

    class Main is Game, MouseMovementListener

        Label label

        action Main
            StartGame()
        end

        action CreateGame
            label:SetSize(20)
            label:SetPosition(20, 20)
            label:SetText("The mouse position is 0, 0")
            Add(label)

            AddMouseMovementListener(me)
        end

        action MovedMouse(MouseEvent event)
            integer x = event:GetX()
            integer y = event:GetY()
            label:SetText("The mouse position is " + x + ", " + y)
        end
    end
    
    */
    action MovedMouse(MouseEvent event)

    end

    /*
    This action is used to respond to a MouseEvent where the mouse was dragged
    (the mouse was moved while a button was held).

    Attribute: Parameter event A MouseEvent describing the mouse drag.

    Attribute: Example

    use Libraries.Game.Game
    use Libraries.Interface.Events.MouseEvent
    use Libraries.Interface.Events.MouseListener
    use Libraries.Interface.Events.MouseMovementListener

    // To try this example, click anywhere on the screen, drag the mouse, and release.
    class Main is Game, MouseListener, MouseMovementListener

        // Variables used to store the sum of the mouse movement between mouse
        // click and mouse release.
        integer sumX = 0
        integer sumY = 0

        action Main
            StartGame()
        end

        action CreateGame
            AddMouseListener(me)
            AddMouseMovementListener(me)
        end

        action ClickedMouse(MouseEvent event)
            sumX = 0
            sumY = 0
        end

        action DraggedMouse(MouseEvent event)
            sumX = sumX + event:GetMovementX()
            sumY = sumY + event:GetMovementY()
        end

        action ReleasedMouse(MouseEvent event)
            output "The total distance moved between mouse clicked and released was " + sumX + ", " + sumY
        end
    end
    */
    action DraggedMouse(MouseEvent event)

    end

end