package Libraries.Interface.Layouts

use Libraries.Interface.Item2D
use Libraries.Containers.Array
use Libraries.Game.Graphics.Label
use Libraries.Interface.Layouts.Layout

class TabFlowLayout is Layout
    
    boolean padFront = false
    boolean ignoreHidden = false
    number padding = 0
    number offset = 0

    action Layout(Item2D item)
        number x = -offset
        number y = 0

        if padFront
            x = padding
        end

        number tallestHeight = 0
        Array<Item2D> kids = item:GetChildren()
        i = 0
        repeat while i < kids:GetSize()
            Item2D kid = kids:Get(i)

            if (not ignoreHidden) or kid:IsShowing()
                if kid:GetHeight() > tallestHeight
                    tallestHeight = kid:GetHeight()
                end

                kid:SetPosition(x, y)
                x = x + kid:GetWidth() + padding
            end
            i = i + 1
        end

        item:SetHeight(tallestHeight)
    end

    action SetPadding(number value)
        padding = value
    end

    action GetPadding returns number
        return padding
    end

    action SetFrontPadding(boolean pad)
        padFront = pad
    end

    action IsFrontPadding returns boolean
        return padFront
    end

    action SetIgnoreHidden(boolean ignore)
        ignoreHidden = ignore
    end

    action IsIgnoringHidden returns boolean
        return ignoreHidden
    end

    action SetOffset(number value)
        offset = value
    end

    action GetOffset returns number
        return offset
    end
end