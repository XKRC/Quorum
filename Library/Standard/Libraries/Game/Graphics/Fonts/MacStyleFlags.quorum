package Libraries.Game.Graphics.Fonts

use Libraries.Compute.BitwiseOperations
use Libraries.Game.Graphics.Fonts.all

/*
The MacStyleFlags class maintains information about the font when being rendered
on Mac operating systems. These flags are found in the "head" table, and more
information can be found on these flags on the Apple TrueType documenation website.

Attribute: Example

    use Libraries.Game.Graphics.Fonts.all
    use Libraries.Game.Game
    use Libraries.System.File
    use Libraries.Containers.ByteArray
    use Libraries.Containers.Support.ByteReader
    use Libraries.System.BinaryFileReader

    class Main is Game
        action Main
            StartGame()
        end

        action CreateGame
            File file
            ByteArray byteArray = undefined
            ByteReader byteReader = undefined
            FontFileReader reader
            // This is the directory for fonts on Mac
            file:SetWorkingDirectory("/Library/Fonts")
            file:SetPath("Arial.ttf")
            reader:Load(file)
            TrueTypeFileInformation fileInformation = reader:GetFileInformation()
            BinaryFileReader binaryReader
            binaryReader:OpenForRead(file)
            byteArray = binaryReader:ReadBytes()
            byteArray:SetBigEndian(true)
            byteReader = byteArray:GetByteReader()
            // This is assuming the current position of the byteReader is at the
            // right location. By default, it will not be.
            integer flags = byteReader:NextTwoByteUnsignedInteger()
            MacStyleFlags macFlags
            macFlags:Set(flags)
        end
    end
*/
class MacStyleFlags 
    /*Whether or not the font is bold.*/
    boolean bold = false
    /*Whether or not the font is italic.*/
    boolean italic = false
    /*Whether or not the font is underlined.*/
    boolean underline = false
    /*Whether or not the font is just an outline of characters.*/
    boolean outline = false
    /*Whether or not the font has shadows.*/
    boolean shadow = false
    /*Whether or not the font is narrow.*/
    boolean condensed = false
    /*Whether or not the font is wide.*/
    boolean extended = false

    /*
    This action sets the Mac Style flags of the font.

    Attribute: Parameter flags The flags of the font.

    Attribute: Example

        use Libraries.Game.Graphics.Fonts.all
        use Libraries.Game.Game
        use Libraries.System.File
        use Libraries.Containers.ByteArray
        use Libraries.Containers.Support.ByteReader
        use Libraries.System.BinaryFileReader

        class Main is Game
            action Main
                StartGame()
            end

            action CreateGame
                File file
                ByteArray byteArray = undefined
                ByteReader byteReader = undefined
                FontFileReader reader
                // This is the directory for fonts on Mac
                file:SetWorkingDirectory("/Library/Fonts")
                file:SetPath("Arial.ttf")
                reader:Load(file)
                TrueTypeFileInformation fileInformation = reader:GetFileInformation()
                BinaryFileReader binaryReader
                binaryReader:OpenForRead(file)
                byteArray = binaryReader:ReadBytes()
                byteArray:SetBigEndian(true)
                byteReader = byteArray:GetByteReader()
                // This is assuming the current position of the byteReader is at the
                // right location. By default, it will not be.
                integer flags = byteReader:NextTwoByteUnsignedInteger()
                MacStyleFlags macFlags
                macFlags:Set(flags)
            end
        end
    */
    action Set(integer flags) 
        BitwiseOperations ops

        bold = ops:And(flags, 1) > 0
        italic = ops:And(flags, 2) > 0
        underline = ops:And(flags, 4) > 0
        outline = ops:And(flags, 8) > 0
        shadow = ops:And(flags, 16) > 0
        condensed = ops:And(flags, 32) > 0
        extended = ops:And(flags, 64) >0
    end
end