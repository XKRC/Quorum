package Libraries.Game.Graphics.Fonts

use Libraries.Containers.HashTable
use Libraries.Game.Graphics.Fonts.all

/*
The KerningValuesList class maintains a hash table which maps a right-hand
character to a kerning value. This hash table is mapped to a given left-hand
character in the KerningTable class.

Attribute: Example

    use Libraries.Game.Graphics.Fonts.all
    use Libraries.Game.Game

    class Main is Game
        action Main
            StartGame()
        end 

        action CreateGame
            KerningValuesList values
            // Gets the kerning value of the glyph at glyph table index 0
            output values:GetKerningValue(0)
        end
    end
*/ 
class KerningValuesList 
    /*Hash table that maps a glyph table character index to a kerning value*/
    HashTable<integer, integer> kerningValuesList

    /*
    This action returns the kerning value for the character at the given
    glyph table index.

    Attribute: Parameter rightCharacterGlyphIndex The glyph table index of the
    right-hand character of the pair.

    Attribute: Returns Returns the kerning value for the characters.

    Attribute: Example

        use Libraries.Game.Graphics.Fonts.all
        use Libraries.Game.Game

        class Main is Game
            action Main
                StartGame()
            end

            action CreateGame
                KerningValuesList values
                output values:GetKerningValue(0)
            end
        end
    */
    action GetKerningValue(integer rightCharacterGlyphIndex) returns integer
        if HasKey(rightCharacterGlyphIndex)    
            return me:kerningValuesList:GetValue(rightCharacterGlyphIndex)
        else
            return 0
        end
    end

    /*
    This action adds the kerning value of the right-hand character to the 
    hash table.

    Attribute: Parameter rightCharacterGlyphIndex The glyph table index of the
    right-hand character of the pair.

    Attribute: Parameter kerningValue The kerning value of the character pair.

    Attribute: Example

        use Libraries.Game.Graphics.Fonts.all
        use Libraries.Game.Game

        class Main is Game
            action Main
                StartGame()
            end

            action CreateGame
                KerningValuesList values
                // Adds a kerning value of 10 to the glyph at glyph table index 0
                // to the hash table.
                values:Add(0, 10)
            end
        end
    */
    action Add(integer rightCharacterGlyphIndex, integer kerningValue)
        me:kerningValuesList:Add(rightCharacterGlyphIndex, kerningValue)
    end

    /*
    This action checks if the kerning value table contains a kerning value for the
    given right-hand character.

    Attribute: Parameter rightCharacterGlyphIndex The glyph table index of the
    right-hand character of the pair.

    Attribute: Returns Returns true if there is a kerning value for the given
    character, and false if there is not.

    Attribute: Example

        use Libraries.Game.Graphics.Fonts.all
        use Libraries.Game.Game

        class Main is Game
            action Main
                StartGame()
            end

            action CreateGame
                KerningValuesList values
                output values:HasKey(0)
            end
        end
    */
    action HasKey(integer rightCharacterGlyphIndex) returns boolean
        return me:kerningValuesList:HasKey(rightCharacterGlyphIndex)
    end
end