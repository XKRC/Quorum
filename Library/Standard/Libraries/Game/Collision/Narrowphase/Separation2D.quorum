package Libraries.Game.Collision.Narrowphase

use Libraries.Compute.Vector2
use Libraries.Game.Collision.Sweep2D
use Libraries.Game.Collision.PhysicsPosition2D
use Libraries.Containers.Array

/*
This action calculates the separation between shapes A and B depending on the 
type of collision that has happened between shapes A and B.
*/
class Separation2D
    constant integer POINTS = 0
    constant integer FACE_A = 1
    constant integer FACE_B = 2

    DistanceProxy2D proxyA
    DistanceProxy2D proxyB
    integer type = POINTS
    Vector2 localPoint
    Vector2 axis
    Sweep2D sweepA
    Sweep2D sweepB

    Vector2 localPointA
    Vector2 localPointB
    Vector2 pointA
    Vector2 pointB
    Vector2 localPointA1
    Vector2 localPointA2
    Vector2 normal
    Vector2 localPointB1
    Vector2 localPointB2
    Vector2 temp
    PhysicsPosition2D transformA
    PhysicsPosition2D transformB

    /*  
    This action sets the DistanceProxy2D that is associated with shape A.
    */
    action SetProxyA(DistanceProxy2D proxy)
        proxyA = proxy
    end

    /*  
    This action sets the DistanceProxy2D that is associated with shape B.
    */
    action SetProxyB(DistanceProxy2D proxy)
        proxyB = proxy
    end

    /*
    This action sets the Sweep2D that is associated with shape A.
    */
    action SetSweepA(Sweep2D sweep)
        sweepA = sweep
    end

    /*
    This action sets the Sweep2D that is associated with shape B.
    */
    action SetSweepB(Sweep2D sweep)
        sweepB = sweep
    end

    action Initialize(SimplexCache2D cache, number t1) returns number
        integer count = cache:GetCount()

        //sweepA:GetTransform(transformA, t1)
        //sweepB:GetTransform(transformB, t1)

        if count = 1
            type = POINTS
            localPointA:Set(proxyA:GetVertex(cache:GetIndexA():Get(0)))
            localPointB:Set(proxyB:GetVertex(cache:GetIndexB():Get(0)))

            pointA:SetX((transformA:GetCosine() * localPointA:GetX() - transformA:GetSine() * localPointA:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPointA:GetX() + transformA:GetCosine() * localPointA:GetY()) + transformA:GetPosition():GetY())
            pointB:SetX((transformB:GetCosine() * localPointB:GetX() - transformB:GetSine() * localPointB:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPointB:GetX() + transformB:GetCosine() * localPointB:GetY()) + transformB:GetPosition():GetY())

            axis:Set(pointB):Subtract(pointA)
            number s = axis:Length()
            axis:Normalize()
            return s
        elseif cache:GetIndexA():Get(0) = cache:GetIndexA():Get(1)
            type = FACE_B
            
            localPointB1:Set(proxyB:GetVertex(cache:GetIndexB():Get(0)))
            localPointB2:Set(proxyB:GetVertex(cache:GetIndexB():Get(1)))

            temp:Set(localPointB2):Subtract(localPointB1)
            axis:SetX(1.0 * temp:GetY())
            axis:SetY(-1.0 * temp:GetX())
            axis:Normalize()

            normal:SetX(transformB:GetCosine() * axis:GetX() - transformB:GetSine() * axis:GetY())
            normal:SetY(transformB:GetSine() * axis:GetX() + transformB:GetCosine() * axis:GetY())

            localPoint:Set(localPointB1):Add(localPointB2):Scale(0.5)            
            pointB:SetX((transformB:GetCosine() * localPoint:GetX() - transformB:GetSine() * localPoint:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPoint:GetX() + transformB:GetCosine() * localPoint:GetY()) + transformB:GetPosition():GetY())

            localPointA:Set(proxyA:GetVertex(cache:GetIndexA():Get(0)))
            pointA:SetX((transformA:GetCosine() * localPointA:GetX() - transformA:GetSine() * localPointA:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPointA:GetX() + transformA:GetCosine() * localPointA:GetY()) + transformA:GetPosition():GetY())

            temp:Set(pointA):Subtract(pointB)
            number s = temp:DotProduct(normal)
            if s < 0
                axis:Scale(-1)
                s = -s
            end
            return s
        else
            type = FACE_A

            localPointA1:Set(proxyA:GetVertex(cache:GetIndexA():Get(0)))
            localPointA2:Set(proxyA:GetVertex(cache:GetIndexA():Get(1)))

            temp:Set(localPointA2):Subtract(localPointA1)
            axis:SetX(1.0 * temp:GetY())
            axis:SetY(-1.0 * temp:GetX())
            axis:Normalize()

            normal:SetX(transformA:GetCosine() * axis:GetX() - transformA:GetSine() * axis:GetY())
            normal:SetY(transformA:GetSine() * axis:GetX() + transformA:GetCosine() * axis:GetY())

            localPoint:Set(localPointA1):Add(localPointA2):Scale(0.5)
            pointA:SetX((transformA:GetCosine() * localPoint:GetX() - transformA:GetSine() * localPoint:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPoint:GetX() + transformA:GetCosine() * localPoint:GetY()) + transformA:GetPosition():GetY())

            localPointB:Set(proxyB:GetVertex(cache:GetIndexB():Get(0)))
            pointB:SetX((transformB:GetCosine() * localPointB:GetX() - transformB:GetSine() * localPointB:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPointB:GetX() + transformB:GetCosine() * localPointB:GetY()) + transformB:GetPosition():GetY())

            temp:Set(pointB):Subtract(pointA)
            number s = temp:DotProduct(normal)
            if s < 0
                axis:Scale(-1)
                s = -s
            end
            return s
        end
    end

    Vector2 axisA
    Vector2 axisB

    action FindMinimumSeparation(Array<integer> indices, number t) returns number
        sweepA:GetTransform(transformA, t)
        sweepB:GetTransform(transformB, t)

        if type = POINTS
            axisA:SetX(transformA:GetCosine() * axis:GetX() + transformA:GetSine() * axis:GetY())
            axisA:SetY(-transformA:GetSine() * axis:GetX() + transformA:GetCosine() * axis:GetY())
            axis:Scale(-1)
            axisB:SetX(transformB:GetCosine() * axis:GetX() + transformB:GetSine() * axis:GetY())
            axisB:SetY(-transformB:GetSine() * axis:GetX() + transformB:GetCosine() * axis:GetY())
            axis:Scale(-1)

            indices:Set(0, proxyA:GetSupport(axisA))
            indices:Set(1, proxyB:GetSupport(axisB))

            localPointA:Set(proxyA:GetVertex(indices:Get(0)))
            localPointB:Set(proxyB:GetVertex(indices:Get(1)))

            pointA:SetX((transformA:GetCosine() * localPointA:GetX() - transformA:GetSine() * localPointA:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPointA:GetX() + transformA:GetCosine() * localPointA:GetY()) + transformA:GetPosition():GetY())
            pointB:SetX((transformB:GetCosine() * localPointB:GetX() - transformB:GetSine() * localPointB:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPointB:GetX() + transformB:GetCosine() * localPointB:GetY()) + transformB:GetPosition():GetY())

            number separation = pointB:Subtract(pointA):DotProduct(axis)
            return separation
        elseif type = FACE_A
            normal:SetX(transformA:GetCosine() * axis:GetX() - transformA:GetSine() * axis:GetY())
            normal:SetY(transformA:GetSine() * axis:GetX() + transformA:GetCosine() * axis:GetY())
            pointA:SetX((transformA:GetCosine() * localPoint:GetX() - transformA:GetSine() * localPoint:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPoint:GetX() + transformA:GetCosine() * localPoint:GetY()) + transformA:GetPosition():GetY())

            normal:Scale(-1)
            axisB:SetX(transformB:GetCosine() * normal:GetX() + transformB:GetSine() * normal:GetY())
            axisB:SetY(-transformB:GetSine() * normal:GetX() + transformB:GetCosine() * normal:GetY())
            normal:Scale(-1)

            indices:Set(0, -1)
            indices:Set(1, proxyB:GetSupport(axisB))

            localPointB:Set(proxyB:GetVertex(indices:Get(1)))
            pointB:SetX((transformB:GetCosine() * localPointB:GetX() - transformB:GetSine() * localPointB:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPointB:GetX() + transformB:GetCosine() * localPointB:GetY()) + transformB:GetPosition():GetY())

            number separation = pointB:Subtract(pointA):DotProduct(normal)
            return separation
        elseif type = FACE_B
            normal:SetX(transformB:GetCosine() * axis:GetX() - transformB:GetSine() * axis:GetY())
            normal:SetY(transformB:GetSine() * axis:GetX() + transformB:GetCosine() * axis:GetY())
            pointB:SetX((transformB:GetCosine() * localPoint:GetX() - transformB:GetSine() * localPoint:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPoint:GetX() + transformB:GetCosine() * localPoint:GetY()) + transformB:GetPosition():GetY())

            normal:Scale(-1)
            axisA:SetX(transformA:GetCosine() * normal:GetX() + transformA:GetSine() * normal:GetY())
            axisA:SetY(-transformA:GetSine() * normal:GetX() + transformA:GetCosine() * normal:GetY())
            normal:Scale(-1)

            indices:Set(1, -1)
            indices:Set(0, proxyA:GetSupport(axisA))

            localPointA:Set(proxyA:GetVertex(indices:Get(0)))
            pointA:SetX((transformA:GetCosine() * localPointA:GetX() - transformA:GetSine() * localPointA:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPointA:GetX() + transformA:GetCosine() * localPointA:GetY()) + transformA:GetPosition():GetY())

            number separation = pointA:Subtract(pointB):DotProduct(normal)
            return separation
        else
            indices:Set(0, -1)
            indices:Set(0, -1)
            return 0
        end
    end

    action Evaluate(integer indexA, integer indexB, number t) returns number
        sweepA:GetTransform(transformA, t)
        sweepB:GetTransform(transformB, t)

        if type = POINTS
            localPointA:Set(proxyA:GetVertex(indexA))
            localPointB:Set(proxyB:GetVertex(indexB))

            pointA:SetX((transformA:GetCosine() * localPointA:GetX() - transformA:GetSine() * localPointA:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPointA:GetX() + transformA:GetCosine() * localPointA:GetY()) + transformA:GetPosition():GetY())
            pointB:SetX((transformB:GetCosine() * localPointB:GetX() - transformB:GetSine() * localPointB:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPointB:GetX() + transformB:GetCosine() * localPointB:GetY()) + transformB:GetPosition():GetY())

            number separation = pointB:Subtract(pointA):DotProduct(axis)
            return separation
        elseif type = FACE_A
            normal:SetX(transformA:GetCosine() * axis:GetX() - transformA:GetSine() * axis:GetY())
            normal:SetY(transformA:GetSine() * axis:GetX() + transformA:GetCosine() * axis:GetY())
            pointA:SetX((transformA:GetCosine() * localPoint:GetX() - transformA:GetSine() * localPoint:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPoint:GetX() + transformA:GetCosine() * localPoint:GetY()) + transformA:GetPosition():GetY())

            localPointB:Set(proxyB:GetVertex(indexB))
            pointB:SetX((transformB:GetCosine() * localPointB:GetX() - transformB:GetSine() * localPointB:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPointB:GetX() + transformB:GetCosine() * localPointB:GetY()) + transformB:GetPosition():GetY())

            number separation = pointB:Subtract(pointA):DotProduct(normal)
            return separation
        elseif type = FACE_B
            normal:SetX(transformB:GetCosine() * axis:GetX() - transformB:GetSine() * axis:GetY())
            normal:SetY(transformB:GetSine() * axis:GetX() + transformB:GetCosine() * axis:GetY())
            pointB:SetX((transformB:GetCosine() * localPoint:GetX() - transformB:GetSine() * localPoint:GetY()) + transformB:GetPosition():GetX())
            pointB:SetY((transformB:GetSine() * localPoint:GetX() + transformB:GetCosine() * localPoint:GetY()) + transformB:GetPosition():GetY())

            localPointA:Set(proxyA:GetVertex(indexA))
            pointA:SetX((transformA:GetCosine() * localPointA:GetX() - transformA:GetSine() * localPointA:GetY()) + transformA:GetPosition():GetX())
            pointA:SetY((transformA:GetSine() * localPointA:GetX() + transformA:GetCosine() * localPointA:GetY()) + transformA:GetPosition():GetY())

            number separation = pointA:Subtract(pointB):DotProduct(normal)
            return separation
        else
            return 0
        end
    end
end