package Libraries.Compute.MatrixTransform

use Libraries.Compute.MatrixTransform.MatrixTransform
use Libraries.Compute.Matrix

/*
    This is a MatrixTransform class to flip a Matrix either vertically or horizontally. 

    Attribute Returns A new matrix after the operation is performed.

    Attribute: Example

    use Libraries.Compute.Matrix
    use Libraries.Compute.MatrixTransform.Flip

    class Main 
        action Main
            Matrix matrix
            matrix:Fill(3, 3, 0.0)
            matrix:SetRow(0, 1.1)
            matrix:SetRow(1, 2.1)
            matrix:SetRow(2, 3.1)
            Flip flip
            flip:FlipVertical()
            Matrix result = flip:Transform(matrix)
            output result:ToText()
        end
    end
*/
class Flip is MatrixTransform
    private constant integer VERTICAL = 1
    private constant integer HORIZONTAL = -1
    private integer direction = 0

/*
    This sets the transform operation to flip vertical.

    Attribute: Example

    use Libraries.Compute.Matrix
    use Libraries.Compute.MatrixTransform.Flip

    class Main 
        action Main
            Flip flip
            flip:FlipVertical()
        end
    end
*/
    action FlipVertical()
        direction = VERTICAL
    end

/*
    This sets the transform operation to flip horizontal.

    Attribute: Example

    use Libraries.Compute.Matrix
    use Libraries.Compute.MatrixTransform.Flip

    class Main 
        action Main
            Flip flip
            flip:FlipHorizontal()
        end
    end
*/
    action FlipHorizontal()
        direction = HORIZONTAL
    end

/*
    This method applies the transform to the Matrix. 

    Attribute Parameter The matrix to transform.

    Attribute Returns A new matrix after the operation is performed.

    Attribute: Example

    use Libraries.Compute.Matrix
    use Libraries.Compute.MatrixTransform.Flip

    class Main 
        action Main
            Matrix matrix
            matrix:Fill(3, 3, 0.0)
            matrix:SetColumn(0, 1.1)
            matrix:SetColumn(1, 2.1)
            matrix:SetColumn(2, 3.1)
            Flip flip
            flip:FlipHorizontal()
            Matrix result = flip:Transform(matrix)
            output result:ToText()
        end
    end
*/
    action Transform(Matrix matrix) returns Matrix
        Matrix newMatrix
        newMatrix:SetSize(matrix)
        row = 0
        repeat matrix:GetRows() times
            col = 0
            repeat matrix:GetColumns() times
                if direction = HORIZONTAL
                    newMatrix:Set(row, col, matrix:Get(row, matrix:GetColumns()-col-1))
                else
                    newMatrix:Set(row, col, matrix:Get(matrix:GetRows()-row-1, col))
                end
                col = col + 1
            end
            row = row + 1
        end
        return newMatrix
    end
end