package Libraries.Compute.MatrixTransform

use Libraries.Compute.MatrixTransform.MatrixTransform
use Libraries.Compute.Matrix

/*
    This is a MatrixTransform class to reshape the size of a Matrix and fill empty values. 

    Attribute Returns A new matrix after the operation is performed.

    Attribute Parameter offsetRow The row offset for the first element in the new matrix.
    Attribute Parameter offsetColumn The column offset for the first element in the new matrix.
    Attribute Parameter rows The number of rows to extract.
    Attribute Parameter columns The number of columns to extract.
    Attribute Parameter fillValue The value to fill in any undefined cells in the new matrix.

    Attribute Returns A new matrix after the operation is performed.

    Attribute: Example

    use Libraries.Compute.Matrix
    use Libraries.Compute.MatrixTransform.Reshape

    class Main 
        action Main
            Matrix matrix
            matrix:Fill(4, 4, 0.0)
            matrix:SetColumn(0, 1.1)
            matrix:SetColumn(1, 2.1)
            matrix:SetColumn(2, 3.1)
            matrix:SetColumn(3, 4.1)
            Reshape reshape
            reshape:SetValues(1, 1, 6, 6, 0.0)
            Matrix result = reshape:Transform(matrix)
            output result:ToText()
        end
    end
*/

class Reshape is MatrixTransform
    private integer offsetColumn = 0
    private integer offsetRow = 0
    private integer columns = 0
    private integer rows = 0
    private number fillValue = 0.0

    /*
        This method sets all the value parameters for the matrix transform. 

        Attribute Parameter offsetRow The row offset for the first element in the new matrix.
        Attribute Parameter offsetColumn The column offset for the first element in the new matrix.
        Attribute Parameter rows The number of rows to extract.
        Attribute Parameter columns The number of columns to extract.
        Attribute Parameter fillValue The value to fill in any undefined cells in the new matrix.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetValues(1, 1, 6, 6, 0.0)
            end
        end
    */
    action SetValues(integer offsetRow, integer offsetColumn, integer rows, integer columns, number fillValue)
        me:offsetRow = offsetRow
        me:offsetColumn = offsetColumn
        me:columns = columns
        me:rows = rows
        me:fillValue = fillValue
    end

    /*
        This method sets the offsetColumn for the matrix transform. 

        Attribute Parameter offsetColumn The column offset for the first element in the new matrix.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetOffsetColumn(1)
                output reshape:GetOffsetColumn()
            end
        end
    */
    action SetOffsetColumn(integer offsetColumn)
        me:offsetColumn = offsetColumn
    end

    /*
        This method sets the offsetRow for the matrix transform. 

        Attribute Parameter offsetRow The row offset for the first element in the new matrix.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetOffsetRow(1)
                output reshape:GetOffsetRow()
            end
        end
    */
    action SetOffsetRow(integer offsetRow)
        me:offsetRow = offsetRow
    end

    /*
        This method sets the columns for the matrix transform. 

        Attribute Parameter columns The number of columns to extract.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetColumns(6)
                output reshape:GetColumns()
            end
        end
    */
    action SetColumns(integer columns)
        me:columns = columns
    end

    /*
        This method sets the rows for the matrix transform. 

        Attribute Parameter rows The number of rows to extract.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetRows(6)
                output reshape:GetRows()
            end
        end
    */
    action SetRows(integer rows)
        me:rows = rows
    end

    /*
        This method sets the FillValue of the matrix transform. 

        Attribute Parameter The FillValue of the matrix transform.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetFillValue(0.0)
                output reshape:GetFillValue()
            end
        end
    */
    action SetFillValue(number fillValue)
        me:fillValue = fillValue
    end

    /*
        This method gets the offsetColumn for the matrix transform. 

        Attribute Returns The column offset for the first element in the new matrix.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetOffsetColumn(1)
                output reshape:GetOffsetColumn()
            end
        end
    */
    action GetOffsetColumn() returns integer
        return me:offsetColumn
    end

    /*
        This method gets the offsetRow for the matrix transform. 

        Attribute Returns The row offset for the first element in the new matrix.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetOffsetRow(1)
                output reshape:GetOffsetRow()
            end
        end
    */
    action GetOffsetRow() returns integer
        return me:offsetRow
    end

    /*
        This method get the number of columns for the matrix transform. 

        Attribute Returns The number of columns to extract.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetColumns(6)
                output reshape:GetColumns()
            end
        end
    */
    action GetColumns() returns integer
        return me:columns
    end

    /*
        This method gets the number of rows for the matrix transform. 

        Attribute Returns The number of rows to extract.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetRows(6)
                output reshape:GetRows()
            end
        end
    */
    action GetRows() returns integer
        return me:rows
    end

    /*
        This method returns the FillValue of the matrix transform. 

        Attribute Returns The FillValue of the matrix transform.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Reshape reshape
                reshape:SetFillValue(0.0)
                output reshape:GetFillValue()
            end
        end
    */
    action GetFillValue() returns number
        return me:fillValue
    end

    /*
        This method applies the transform to the Matrix. 

        Attribute Parameter The matrix to transform.

        Attribute Returns A new matrix after the operation is performed.

        Attribute: Example

        use Libraries.Compute.Matrix
        use Libraries.Compute.MatrixTransform.Reshape

        class Main 
            action Main
                Matrix matrix
                matrix:Fill(4, 4, 0.0)
                matrix:SetColumn(0, 1.1)
                matrix:SetColumn(1, 2.1)
                matrix:SetColumn(2, 3.1)
                matrix:SetColumn(3, 4.1)
                Reshape reshape
                reshape:SetValues(1, 1, 6, 6, 0.0)
                Matrix result = reshape:Transform(matrix)
                output result:ToText()
            end
        end
    */
    action Transform(Matrix matrix) returns Matrix
        Matrix resultMatrix
        resultMatrix:Fill(rows, columns, fillValue)

        integer startColumn = 0
        integer startRow = 0
        if offsetColumn > 0
            startColumn = offsetColumn
        end
        if offsetRow > 0
            startRow = offsetRow
        end

        integer endColumn = columns
        integer endRow = rows
        if endColumn > matrix:GetColumns() + offsetColumn
            endColumn = matrix:GetColumns() + offsetColumn
        end
        if endRow > matrix:GetRows() + offsetRow
            endRow = matrix:GetRows() + offsetRow
        end

        integer col = startColumn
        integer row = startRow
        repeat while row < endRow
            col = startColumn
            repeat while col < endColumn
                resultMatrix:Set(row, col, matrix:Get(row - offsetRow, col - offsetColumn))
                col = col + 1
            end
            row = row + 1
        end
        return resultMatrix
    end
end